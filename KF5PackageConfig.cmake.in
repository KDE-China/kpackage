@PACKAGE_INIT@

# Any changes in this ".cmake" file will be overwritten by CMake, the source is the ".cmake.in" file.

include("${CMAKE_CURRENT_LIST_DIR}/KF5PackageTargets.cmake")
@PACKAGE_INCLUDE_QCHTARGETS@

set(Package_INSTALL_PREFIX "@PACKAGE_CMAKE_INSTALL_PREFIX@")
set_and_check(Package_INCLUDE_DIR    "@PACKAGE_KF5_INCLUDE_INSTALL_DIR@")

set(Package_LIBRARIES KF5::Package)

include(CMakeFindDependencyMacro)
find_dependency(KF5CoreAddons "@KF5_DEP_VERSION@")

if(NOT CMAKE_VERSION VERSION_LESS "3.10.0")
    # CMake 3.9+ warns about automoc on files without Q_OBJECT, and doesn't know about other macros.
    # 3.10+ lets us provide more macro names that require automoc.
    list(APPEND CMAKE_AUTOMOC_MACRO_NAMES @KPackage_AUTOMOC_MACRO_NAMES@)
endif()
if(NOT CMAKE_VERSION VERSION_LESS "3.9.0")
    foreach(macro_name  @KPackage_AUTOMOC_MACRO_NAMES_WITH_JSON_ARG2@)
        # tell automoc how to find names of plugin metadata files
        list(APPEND CMAKE_AUTOMOC_DEPEND_FILTERS
            "${macro_name}"
            "[\n^][ \t]*${macro_name}[ \t\n]*\\([^,]*,[ \t\n]*\"([^\"]+)\""
        )
    endforeach()
endif()

include("${CMAKE_CURRENT_LIST_DIR}/KF5PackageMacros.cmake")
